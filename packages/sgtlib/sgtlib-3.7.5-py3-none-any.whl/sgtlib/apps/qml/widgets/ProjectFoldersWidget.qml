import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtQuick.Controls.Basic as Basic
import Theme 1.0


ColumnLayout {
    id: projectFoldersControls
    Layout.preferredHeight: 90
    Layout.preferredWidth: parent.width
    Layout.alignment: Qt.AlignTop
    Layout.topMargin: 10
    Layout.leftMargin: 10
    Layout.rightMargin: 5
    spacing: 5

    property int txtWidthSize: 170

    RowLayout {
        id: rowLayoutProject
        visible: projectController.is_project_open()

        Label {
            text: "Project Name:"
            font.bold: true
            color: Theme.text
        }

        Text {
            id: txtProjectName
            Layout.minimumWidth: txtWidthSize
            Layout.fillWidth: true
            Layout.alignment: Qt.AlignLeft
            text: ""
            color: Theme.text
            wrapMode: Text.NoWrap
            elide: Text.ElideRight
            maximumLineCount: 1        // ensures single-line behavior
            font.pixelSize: 10
            verticalAlignment: Text.AlignVCenter
            horizontalAlignment: Text.AlignLeft
            clip: true
        }

    }

    RowLayout {
        Label {
            text: "Output Dir:"
            font.bold: true
            color: Theme.text
        }

        Rectangle {
            Layout.minimumWidth: txtWidthSize
            Layout.fillWidth: true
            Layout.alignment: Qt.AlignLeft
            implicitHeight: 24
            border.width: 1
            border.color: Theme.gray
            radius: 5
            color: "transparent"

            Text {
                id: txtOutputDir
                anchors.fill: parent
                anchors.margins: 4
                text: ""
                color: Theme.text
                wrapMode: Text.NoWrap
                elide: Text.ElideRight
                font.pixelSize: 10
                verticalAlignment: Text.AlignVCenter
                horizontalAlignment: Text.AlignLeft
                clip: true
            }
        }

        Basic.Button {
            id: btnChangeOutDir
            //text: "Change"
            icon.source: "../assets/icons/edit_icon.png"
            icon.width: 21
            icon.height: 21
            icon.color: enabled ? Theme.black : Theme.disabled
            background: Rectangle {
                color: "transparent"
            }
            enabled: imageController.display_image()
            onClicked: dialogFolderOutput.open()
        }
    }

    Button {
        id: btnImportImages
        text: "Import image(s)"
        leftPadding: 10
        rightPadding: 10
        Layout.alignment: Qt.AlignHCenter
        enabled: imageController.display_image()
        onClicked: dialogFileImage.open()
    }


    Connections {
        target: mainController

        function onImageChangedSignal() {
            // Force refresh
            txtOutputDir.text = projectController.get_output_dir();
            btnChangeOutDir.enabled = imageController.display_image();
            btnImportImages.enabled = imageController.display_image() || projectController.is_project_open();
        }
    }

    Connections {
        target: projectController

        function onProjectOpenedSignal(name) {
            txtProjectName.text = name;
            rowLayoutProject.visible = projectController.is_project_open();
            btnImportImages.enabled = imageController.display_image() || projectController.is_project_open();
        }
    }
}